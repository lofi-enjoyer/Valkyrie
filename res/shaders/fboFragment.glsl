#version 460
out vec4 FragColor;

in vec2 TexCoords;

uniform sampler2D screenTexture;
uniform float saturation;
uniform float gamma;

const float Epsilon = 1e-10;

vec3 RGBtoHSV(vec3 RGB)
{
    vec4  P   = (RGB.g < RGB.b) ? vec4(RGB.bg, -1.0, 2.0/3.0) : vec4(RGB.gb, 0.0, -1.0/3.0);
    vec4  Q   = (RGB.r < P.x) ? vec4(P.xyw, RGB.r) : vec4(RGB.r, P.yzx);
    float C   = Q.x - min(Q.w, Q.y);
    float H   = abs((Q.w - Q.y) / (6.0 * C + Epsilon) + Q.z);
    vec3  HCV = vec3(H, C, Q.x);
    float S   = HCV.y / (HCV.z + Epsilon);
    return vec3(HCV.x, S, HCV.z);
}

vec3 HSVtoRGB(vec3 HSV)
{
    float H   = HSV.x;
    float R   = abs(H * 6.0 - 3.0) - 1.0;
    float G   = 2.0 - abs(H * 6.0 - 2.0);
    float B   = 2.0 - abs(H * 6.0 - 4.0);
    vec3  RGB = clamp( vec3(R,G,B), 0.0, 1.0 );
    return ((RGB - 1.0) * HSV.y + 1.0) * HSV.z;
}

void main()
{
    vec4 color = texture(screenTexture, TexCoords);
    vec3 col_hsv = RGBtoHSV(color.rgb);
    col_hsv.y *= saturation;
    vec3 col_rgb = HSVtoRGB(col_hsv.rgb);

    FragColor = vec4(pow(col_rgb.rgb, vec3(1.0/gamma)), color.a);
}