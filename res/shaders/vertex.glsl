#version 460

layout (location = 0) in uint data;
layout (location = 1) in uvec2 position;

out vec2 textureCoords;
out vec4 outData;
out vec4 passLight;
out vec3 outCamPos;
out vec2 texOffset;

uniform mat4 proj;
uniform mat4 view;
uniform float dayTime;
uniform vec3 lightDir;
uniform float light;
uniform float worldTime;
uniform mat4 shadowProj;
uniform mat4 shadowView;
uniform ivec3 camChunkPos;
uniform int triangleSizeMultiplier;

layout(packed, binding = 0) buffer positionBuffer
{
    int positionData[];
};

const float shadow[6] = {
0.875f,
0.875f,
0.75f,
0.75f,
1.0f,
0.5f
};

const vec3 normal[6] = {
vec3(0, 0, 1),
vec3(0, 0, -1),
vec3(-1, 0, 0),
vec3(1, 0, 0),
vec3(0, 1, 0),
vec3(0, -1, 0)
};

const vec3 tangents[6] = {
vec3(1, 0, 0),
vec3(-1, 0, 0),
vec3(0, 0, 1),
vec3(0, 0, -1),
vec3(1, 0, 0),
vec3(1, 0, 0)
};

out VS_OUT {
    vec3 FragPos;
    vec3 Normal;
    vec2 TexCoords;
    vec4 FragPosLightSpace;
    mat3 tbn;
} vs_out;

vec3 getChunkPosition(int index) {
    return vec3(positionData[index * 2] & 0xffff, (positionData[index * 2] >> 16) & 0xffff, positionData[index * 2 + 1] & 0xffff);
}

void main()
{
    int x = int(data >> 5) * triangleSizeMultiplier;
    int y = int(data & 0x1fu) * triangleSizeMultiplier - triangleSizeMultiplier + 1;
    vec3 chunkPosition = getChunkPosition(gl_DrawID) - camChunkPos;
    int face = int((position.x >> 20) & 0x7u);
    float positionX = int((position.x >> 8) & 0x3fu);
    float positionY = int(position.x & 0xffu);
    float positionZ = int((position.x >> 14) & 0x3fu);
    int width = int((position.x >> 23) & 0x7fu) + 1;
    int height = int((position.y >> 20) & 0x7fu) + 1;
    int blockLight = int(position.y & 0xfffu);
    int texture = (int(position.y >> 12) & 0xff);

    if (face == 0) {
        positionY++;
    } else if (face == 1) {
        positionY++;
        positionZ++;
    } else if (face == 2) {
        positionY++;
        positionX++;
    } else if (face == 3) {
        positionY++;
    } else if (face == 4) {

    } else if (face == 5) {
        positionY++;
    }

    texOffset = vec2(0);
    if (texture == 3 || texture == 7 || texture == 8 || texture == 9) {
        texOffset.x += (sin(worldTime * 4) + 1) / 16;
        texOffset.y += (sin((worldTime + 2) * 20) + 1) / 32;
    }

    float offsetX = positionX + chunkPosition.x * 32;
    float offsetY = positionY - 1;
    float offsetZ = positionZ + chunkPosition.z * 32;

    if (face == 0) {
        gl_Position = vec4(x * width + offsetX, y * height + offsetY, offsetZ + 1, 1.0);
    } else if (face == 1) {
        gl_Position = vec4((1 - x) * width + offsetX, y * height + offsetY, offsetZ, 1.0);
    } else if (face == 2) {
        gl_Position = vec4(offsetX, y * height + offsetY, x * width + offsetZ, 1.0);
    } else if (face == 3) {
        gl_Position = vec4(offsetX + 1, y * height + offsetY, (1 - x) * width + offsetZ, 1.0);
    } else if (face == 4) {
        gl_Position = vec4(x * width + offsetX, offsetY + 1, (1 - y) * height + offsetZ, 1.0);
    } else if (face == 5) {
        gl_Position = vec4(x * width + offsetX, offsetY, y * height + offsetZ, 1.0);
    }
    textureCoords = vec2(x, y + triangleSizeMultiplier - 1);
    outData = vec4(x * width, y * height, float(texture), shadow[face]);

    float dotProduct = max(dot(normal[face], lightDir), 0.85);
    passLight = vec4((blockLight >> 9) & 0x7, (blockLight >> 6) & 0x7, (blockLight >> 3) & 0x7, blockLight & 0x7) / 7.0;
    float s = max(0.125, passLight.a * light * dotProduct);

    vec3 faceNormal = normal[face];
    vec3 tangent = tangents[face];
    vec3 bitangent = cross(faceNormal, tangent);
    mat3 tbn = mat3(tangent, bitangent, faceNormal);

    vs_out.FragPos = vec3(gl_Position.xyz);
    vs_out.Normal = normal[face];
    vs_out.tbn = tbn;
    vs_out.FragPosLightSpace = shadowProj * shadowView * vec4(vs_out.FragPos, 1.0);
    gl_Position = proj * view * vec4(vs_out.FragPos, 1.0);
    outCamPos = camChunkPos;

}